#version 460
#extension GL_EXT_ray_tracing : enable

#include <Raytracing/Random.glsl>
#include <Raytracing/CameraProperties.glsl>

layout(set = 0, binding = 0) uniform accelerationStructureEXT tlas;
layout(set = 0, binding = 1, rgba8) uniform image2D storageImage;
layout(set = 0, binding = 2, rgba32f) uniform image2D accumulationImage;
layout(set = 0, binding = 3) uniform Camera { CameraProperties cam; };

layout(location = 0) rayPayloadEXT vec3 hitValue;
//layout(location = 3) rayPayloadEXT uint payloadSeed;

const float RANDOM_RADIUS = 1.0;
const float T_MIN = 0.001;
const float T_MAX = 10000.0;

void main()
{
	uint seed = TEA(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, cam.frame);
	vec2 random2 = vec2(RND(seed), RND(seed)) * RANDOM_RADIUS;

	vec2 subpixelJitter = cam.frame == 0 ? vec2(0.5f, 0.5f) : random2;
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + subpixelJitter;
	const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = cam.viewInverse * vec4(0, 0, 0, 1);
	vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1);
	vec4 direction = cam.viewInverse * vec4(normalize(target.xyz), 0);

	hitValue = vec3(0.0);
	
	traceRayEXT(tlas, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, T_MIN, direction.xyz, T_MAX, 0);

	if (cam.frame > 0)
	{
		float a = 1.0f / float(cam.frame + 1);
		vec3 oldColor = imageLoad(storageImage, ivec2(gl_LaunchIDEXT.xy)).xyz;
		imageStore(storageImage, ivec2(gl_LaunchIDEXT.xy), vec4(mix(oldColor, hitValue, a), 1.f));
	}
	else
	{
		// First frame, replace the value in the buffer
		imageStore(storageImage, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.f));
	}
}
